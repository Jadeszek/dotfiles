
#
# .zsh/keys
# Maintainer:	Philipp Millar <philipp.millar@gmx.de>
#

# zle-functions {{{
# prepend sudo
run-with-sudo () { LBUFFER="sudo $LBUFFER" }
zle -N run-with-sudo

typeset -Ag abbreviations
abbreviations=(
  "Il"    "| less"
  "Ia"    "| awk"
  "Ig"    "| grep"
  "Igg"   "| ack"
  "Ip"    "| $PAGER"
  "Ih"    "| head"
  "It"    "| tail"
  "Is"    "| sort"
  "Iv"    "| ${VISUAL:-${EDITOR}}"
  "Iw"    "| wc"
  "Ix"    "| xargs"
)

magic-abbrev-expand() {
    local MATCH
    LBUFFER=${LBUFFER%%(#m)[_a-zA-Z0-9]#}
    LBUFFER+=${abbreviations[$MATCH]:-$MATCH}
    zle magic-space
}

no-magic-abbrev-expand() {
  LBUFFER+=' '
}

zle -N magic-abbrev-expand
zle -N no-magic-abbrev-expand
#}}}
# vi command mode {{{
bindkey -M vicmd "gg" beginning-of-history
bindkey -M vicmd "G"  end-of-history
bindkey -M vicmd "k"  history-search-backward
bindkey -M vicmd "j"  history-search-forward
bindkey -M vicmd "/"  history-incremental-pattern-search-backward
bindkey -M vicmd "?"  history-incremental-pattern-search-forward

bindkey -M vicmd u undo
bindkey -M vicmd '^R' redo
# }}}
# vi insert mode {{{
bindkey -M viins "^L" clear-screen
bindkey -M viins "^W" backward-kill-word
bindkey -M viins "^A" beginning-of-line
bindkey -M viins "^E" end-of-line

bindkey -M viins '^R' history-incremental-pattern-search-backward
bindkey -M viins "\e[A" history-search-backward
bindkey -M viins "\e[B" history-search-forward
bindkey -M viins '^P' history-search-backward
bindkey -M viins '^N' history-search-forward  

bindkey -M viins '^Z' undo
bindkey -M viins '^Y' redo

bindkey -M viins   '^S' run-with-sudo
bindkey -M viins   " " magic-abbrev-expand
bindkey -M viins   "^x " no-magic-abbrev-expand
bindkey -M isearch " " self-insert
# }}}
# completion {{{
# navigation
bindkey -M menuselect 'h' backward-char
bindkey -M menuselect 'j' down-line-or-history
bindkey -M menuselect 'k' up-line-or-history
bindkey -M menuselect 'l' forward-char
# insert, but accept further completions
bindkey -M menuselect 'i' accept-and-menu-complete
# insert, and show menu with further possible completions
bindkey -M menuselect 'o' accept-and-next-history
# undo
bindkey -M menuselect 'u' undo
# }}}

# vim:set sw=4 foldmethod=marker ft=zsh:
