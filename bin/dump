#!/usr/bin/env zsh

source ~/.config/dump/ssh.conf

LOG_FILE=$HOME/.log/duplicity.log

DUP_SSH=scp://${DUP_SSH_USER}@${DUP_SSH_HOST}/${DUP_SSH_DIR}

function say() {
  print "[34;1m::[0;37m $*"
}

function quit() {
  say 'Cleaning Up...'
  rm -f ~/tmp/pkglist ~/tmp/aurlist
  exit $1
}

function die() {
  print "[31;1m::[0;37m $*"
  quit 1
}

function TRAPINT() {
  die "Caught SIGINT, aborting."
}

# actually run the backup

say 'Creating temporary files..'
comm -23 <(pacman -Qeq | sort) <(pacman -Qmq | sort) >~/tmp/pkglist
pacman -Qmq | sort >~/tmp/aurlist

say "Ping backup server $DUP_SSH_HOST.."
if ping -c 1 $DUP_SSH_HOST &>/dev/null; then

  say 'Uploding backup..'
  duplicity \
    --full-if-older-than $DUP_FULL_AFTER \
    --asynchronous-upload \
    --no-encryption \
    --log-file ${LOG_FILE} \
    $HOME ${DUP_SSH}

  say 'Deleting old backups..'
  duplicity --force remove-all-but-n-full 6 ${DUP_SSH}
  duplicity --force remove-all-inc-of-but-n-full 2 ${DUP_SSH}

  # remove temporary files
  quit 0

else
  die "Could not connect to server"
fi

