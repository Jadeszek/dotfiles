#!/usr/bin/env zsh
emulate zsh

# where to install to?
target="${target:-$HOME}"
# dependencies of this script
make_dependencies=(stow zsh)
# options for stow
stow_options=(--target="$target")
logfile=${logfile:-/dev/null}

define_presets() {
  # populate the auto_plugins array
  [[ -d $HOST ]] && auto_plugins=($HOST)

  [[ $OSTYPE =~ ".*gnu.*" ]]     && auto_plugins+=("gnu")
  [[ $OSTYPE =~ ".*freebsd.*" ]] && auto_plugins+=("freebsd")

  which "pacman"   &>/dev/null && auto_plugins+=("archlinux")
  archlinux_depends=(pkgfile)
  which "aptitude" &>/dev/null && auto_plugins+=("aptitude")

  which "ack"      &>/dev/null && auto_plugins+=("ack")
  which "ack-grep" &>/dev/null && auto_plugins+=("ack")
  which "aunpack"  &>/dev/null && auto_plugins+=("atool")
  atool_depend=(pigz pbzip2 colordiff)
  which "git"      &>/dev/null && auto_plugins+=("git")
  git_depends=(tig)
  which "hg"       &>/dev/null && auto_plugins+=("hg")
  which "keychain" &>/dev/null && auto_plugins+=("keychain")
  which "svn"      &>/dev/null && auto_plugins+=("svn")
  which "tmux"     &>/dev/null && auto_plugins+=("tmux")
  tmux_depends=(urlview)

  # definition of the presets
  small=(base $auto_plugins)
  huge=($small xorg)

  # populate the huge array
  which openbox   &>/dev/null && huge+=("openbox")
  which ratpoison &>/dev/null && huge+=("ratpoison")

  # dependecies
  base_depends=(less dfc)
  vim_depends=(vim ctags python)

  # TODO: there are more than those
  xorg_depends=(autorandr xbindkeys openbox)
  openbox_depends=(udevedu nitrogen compton stalonetray conky pasystray)
  ratpoison_depends=(udevedu nitrogen compton stalonetray conky pasystray)

  # remove duplicates
  typeset -U small medium huge auto_plugins
}

#------------------------------------------------------------------------------#
#                               basic functions                                #
#------------------------------------------------------------------------------#

# issue a message and die
die() {
  echo $1
  exit 1
}

# check if a program can be found
check_env() {
  while [[ -n ${1} ]]; do
    if ! which $1 &>/dev/null; then
      missing_dependencies+=(${1})
      ${finish:-echo} "[!] $1 not found"
    fi
    shift
  done
}
check_make_env() {
  finish=die check_env $*
}

# link the plugins with stow
deploy() {
  echo "The following plugins will be installed to $target:"
  echo "$@"
  read -q "?Continue? " || exit 1
  echo ""

  while [[ -n ${1} ]]; do
    # check dependecies
    dependencies=${1}_depends
    [[ -n ${(P)dependencies} ]] && \
      check_env ${(P)dependencies}

    # link
    echo "linking ${1}"
    stow ${stow_options} -R ${1}
    shift
  done
}

# clean up
clean() {
  echo "Cleaning symlinks from $target"
  stow ${stow_options} -D *(/)
}

#------------------------------------------------------------------------------#
#                                     main                                     #
#------------------------------------------------------------------------------#

# cd to basedir of the script
old_pwd=$(pwd)
cd `dirname $0`
# check for missing dependencies
check_make_env $make_dependencies
# load preset definitions
define_presets

# load submodules
git submodule init
git submodule update

# read parameters
case "$1" in
  "small" | "s")
    deploy $small
    ;;
  "huge" | "h")
    deploy $huge
    ;;
  "clean" | "c")
    clean $2
    ;;
  *)
    echo "Usage: $0 {small|huge|clean [-r]}"
    ;;
esac

[[ -n $missing_dependencies ]] && \
  echo "The following executables are missing" &&
  echo "$missing_dependencies"

cd $old_pwd

